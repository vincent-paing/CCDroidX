// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath(AndroidXNavigation.gradle_plugin)
    classpath(DaggerHilt.gradle_plugin)
    classpath(Wire.gradle_plugin)
    classpath(AndroidJUnit5.gradle_plugin)
    classpath 'com.google.gms:google-services:4.3.15'
    classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.4'
  }
}

plugins {
  id 'com.android.application' version '7.4.2' apply false
  id 'com.android.library' version '7.4.2' apply false
  id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
  id 'com.squareup.sqldelight' version '1.5.5' apply false
  id "com.github.ben-manes.versions" version "0.46.0"
}

tasks.register('clean', Delete) {
  delete rootProject.buildDir
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}